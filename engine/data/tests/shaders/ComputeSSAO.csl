/***
* Boomer Engine v4 2015-2017
* Written by Tomasz "Rex Dex" Jonarski
***/

//---

#include "common.h"

descriptor TestParams
{
    ConstantBuffer
    {
        mat4 PixelCoordToWorld;
    }

    attribute(nosampler, format=r32f) Texture2D LinearizedDepth;
	attribute(uav, format=rgba8) Texture2D OutTexture;
}

//--

const int NUM_STEPS = 4;
const int NUM_DIRECTIONS = 8;
const float Alpha = 2.0 * PI / NUM_DIRECTIONS;

export shader SSAOCS
{
    vec3 PosToWorld(ivec2 pixelCoord, float linearZ)
    {
        vec4 positionWS = vec4(pixelCoord, linearZ, 1.0f) * PixelCoordToWorld;
        return positionWS.xyz / positionWS.w;
    }

    vec3 FetchViewPos(ivec2 pixelCoord)
    {
        float depth = LinearizedDepth[pixelCoord];
        return PosToWorld(pixelCoord, depth);
    }

    vec3 MinDiff(vec3 P, vec3 Pr, vec3 Pl)
    {
        vec3 V1 = Pr - P;
        vec3 V2 = P - Pl;
        return (dot(V1,V1) < dot(V2,V2)) ? V1 : V2;
    }

    vec3 ReconstructNormal(ivec2 pixelCoord, vec3 P)
    {
        vec3 Pr = FetchViewPos(pixelCoord + ivec2(1, 0));
        vec3 Pl = FetchViewPos(pixelCoord + ivec2(-1, 0));
        vec3 Pt = FetchViewPos(pixelCoord + ivec2(0, 1));
        vec3 Pb = FetchViewPos(pixelCoord + ivec2(0, -1));
        return normalize(cross(MinDiff(P, Pr, Pl), MinDiff(P, Pt, Pb)));
    }

    const float controlNDotVBias = 0.0f;
    const float controlNegInvR2 = -1.0f / 2.0f;
    const float controlRadiusToScreen = 32.0f;

    float RandFloat(vec2 co)
    {
        return frac(sin(dot(co.xy, vec2(12.9898,78.233))) * 43758.5453);
    }

    float Falloff(float DistanceSquare)
    {
        // 1 scalar mad instruction
        return DistanceSquare * controlNegInvR2 + 1.0;
    }

    float ComputeAO(vec3 P, vec3 N, vec3 S)
    {
        vec3 V = S - P;
        float VdotV = dot(V, V);
        float NdotV = dot(N, V) * 1.0/sqrt(VdotV);

        // Use saturate(x) instead of max(x,0.f) because that is faster on Kepler
        return clamp(NdotV - controlNDotVBias,0,1) * saturate(Falloff(VdotV));
    }

    vec2 RotateDirection(vec2 Dir, vec2 CosSin)
    {
        return vec2(Dir.x*CosSin.x - Dir.y*CosSin.y, Dir.x*CosSin.y + Dir.y*CosSin.x);
    }

    vec4 GetJitter(vec2 co)
    {
        float rand = RandFloat(co) * PI * 2.0f;
        float randz = RandFloat(co*2);
        float randw = RandFloat(co*2.1234);
        return vec4(cos(rand), sin(rand), randz, randw);
    }

    float ComputeCoarseAO(ivec2 FullResUV, float RadiusPixels, vec4 Rand, vec3 ViewPosition, vec3 ViewNormal)
    {
        // Divide by NUM_STEPS+1 so that the farthest samples are not fully attenuated
        float StepSizePixels = 25.0f / (NUM_STEPS + 1);

        float AO = 0;
        for (int DirectionIndex = 0; DirectionIndex < NUM_DIRECTIONS; DirectionIndex += 1)
        {
            float Angle = Alpha * DirectionIndex;
            //float Angle = 0.0f;

            // Compute normalized 2D direction
            vec2 Direction = RotateDirection(vec2(cos(Angle), sin(Angle)), Rand.xy);
            //return Direction.y - Direction.x;

            // Jitter starting sample within the first step
            float RayPixels = (Rand.z * StepSizePixels + 1.0);
            for (int StepIndex = 0; StepIndex < NUM_STEPS; StepIndex += 1)
            {
                ivec2 SnappedUV = FullResUV + (RayPixels * Direction);
                vec3 S = FetchViewPos(SnappedUV);
                RayPixels += StepSizePixels;

                AO += ComputeAO(ViewPosition, ViewNormal, S);
            }
        }

        AO /= (NUM_DIRECTIONS * NUM_STEPS);
        return saturate(1.0 - AO * 2.0f);
    }

    attribute(local_size_x=8, local_size_y=8)
    void main()
    {
        vec3 ViewPosition = FetchViewPos(gl_GlobalInvocationID.xy);
        vec3 ViewNormal = -ReconstructNormal(gl_GlobalInvocationID.xy, ViewPosition);

        float LinearDepth = LinearizedDepth[gl_GlobalInvocationID.xy];
        float RadiusPixels = controlRadiusToScreen / LinearDepth;

        vec4 Rand = GetJitter(gl_GlobalInvocationID.xy / 512.0f);
        float AO = ComputeCoarseAO(gl_GlobalInvocationID.xy, RadiusPixels, Rand, ViewPosition, ViewNormal);

        OutTexture[gl_GlobalInvocationID.xy] = AO.xxx1;
    }
};

//--